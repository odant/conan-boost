# Conan wrapper for FindBoost.cmake
# Dmitriy Vetutnev 2018
# ODANT 2018


set(BOOST_ROOT ${CONAN_BOOST_ROOT})
set(BOOST_INCLUDEDIR ${CONAN_INCLUDE_DIRS_BOOST})
set(BOOST_LIBRARYDIR ${CONAN_LIB_DIRS_BOOST})
set(Boost_USE_STATIC_LIBS ON)
set(Boost_NO_SYSTEM_PATHS ON)
set(Boost_NO_BOOST_CMAKE ON)

#set(Boost_DEBUG ON)
set(Boost_DETAILED_FAILURE_MSG ON)

# For CMake version < 3.12
if(WIN32 AND NOT MSVC_TOOLSET_VERSION)

    if(NOT MSVC_VERSION VERSION_LESS 1400 AND MSVC_VERSION VERSION_LESS 1500)
        set(MSVC_TOOLSET_VERSION 80)
    elseif(NOT MSVC_VERSION VERSION_LESS 1500 AND MSVC_VERSION VERSION_LESS 1600)
        set(MSVC_TOOLSET_VERSION 90)
    elseif(NOT MSVC_VERSION VERSION_LESS 1600 AND MSVC_VERSION VERSION_LESS 1700)
        set(MSVC_TOOLSET_VERSION 100)
    elseif(NOT MSVC_VERSION VERSION_LESS 1700 AND MSVC_VERSION VERSION_LESS 1800)
        set(MSVC_TOOLSET_VERSION 110)
    elseif(NOT MSVC_VERSION VERSION_LESS 1800 AND MSVC_VERSION VERSION_LESS 1900)
        set(MSVC_TOOLSET_VERSION 120)
    elseif(NOT MSVC_VERSION VERSION_LESS 1900 AND MSVC_VERSION VERSION_LESS 1910)
        set(MSVC_TOOLSET_VERSION 140)
    elseif(NOT MSVC_VERSION VERSION_LESS 1910 AND MSVC_VERSION VERSION_LESS 1920)
        set(MSVC_TOOLSET_VERSION 141)
    else()
        message(FATAL_ERROR "Conan: Can`t set MSVC_TOOLSET_VERSION, unknown MSVC compiler version [${MSVC_VERSION}]")
    endif()

endif()


include(${CMAKE_CURRENT_LIST_DIR}/_FindBoost.cmake)


# Add defines from package_info
set(Boost_FIND_COMPONENTS boost ${Boost_FIND_COMPONENTS})
foreach(COMPONENT ${Boost_FIND_COMPONENTS})

    if(TARGET Boost::${COMPONENT})
        set_property(TARGET Boost::${COMPONENT} APPEND PROPERTY
            INTERFACE_COMPILE_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_BOOST}
        )
    endif()

endforeach()


include(CMakeFindDependencyMacro)
find_dependency(Threads)

# Add zlib depends
if(TARGET Boost::iostreams)

    find_dependency(ZLIB)

    set_property(TARGET Boost::iostreams APPEND PROPERTY
        INTERFACE_LINK_LIBRARIES ZLIB::ZLIB
    )

endif()

# Add ICU depends
if(TARGET Boost::locale)

    find_dependency(ICU)

    set_property(TARGET Boost::locale APPEND PROPERTY
        INTERFACE_LINK_LIBRARIES ICU::in
    )

endif()
